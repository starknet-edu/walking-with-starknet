# ProgramaÃ§Ã£o no Ethereum L2 (pt. 3): BÃ¡sicas do Cairo 2

Antes de comeÃ§ar, recomendo que vocÃª prepare sua equipe para programar no Cairo â¤ï¸ com o [primeiro tutorial](1_installation.md), e revise o [Cairo basics pt. 1](2_cairo_basics.md).

Este Ã© o terceiro tutorial de uma sÃ©rie focada no desenvolvimento de contratos inteligentes com Cairo e StarkNet.

---

Na terceira parte da sÃ©rie de tutoriais bÃ¡sicos do Cairo, vamos nos aprofundar nos conceitos introduzidos na [segunda sessÃ£o](2_cairo_basics.md) como `builtin`, `felt` e `assert` e suas variaÃ§Ãµes. AlÃ©m disso, vamos introduzir arrays. Com o que aprendemos nesta sessÃ£o poderemos criar contratos bÃ¡sicos no Cairo ğŸš€.

## 1. Builtins e sua relaÃ§Ã£o com pointers 

No programa a seguir estamos multiplicando dois nÃºmeros. O cÃ³digo inteiro estÃ¡ disponÃ­vel em [src/multiplication.cairo](../../../src/multiplication.cairo). LÃ¡ vocÃª encontrarÃ¡ o cÃ³digo corretamente comentado.

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word

func mult_two_nums(num1, num2) -> (prod : felt){
    return(prod = num1 * num2);
}

func main{output_ptr: felt*}(){
    let (prod) = mult_two_nums(2,2);
    serialize_word(prod);
    return ();
}
```

Lembra que introduzimos os `builtins` na Ãºltima sessÃ£o junto com os argumentos implÃ­citos?

Cada `builtin` lhe dÃ¡ o direito de usar um pointer que terÃ¡ o nome do `builtin` + â€œ`_ptr`â€. Por exemplo, o builtin output, que definimos `%builtins output` no inÃ­cio do nosso contrato, nos dÃ¡ o direito de usar o pointer `output_ptr`. O `range_check` embutido nos permite usar o pointer `range_check_ptr`. Esses pointers sÃ£o frequentemente usados como argumentos implÃ­citos que sÃ£o atualizados automaticamente durante uma funÃ§Ã£o.

Na funÃ§Ã£o para multiplicar dois nÃºmeros, usamos `%builtins output` e entÃ£o usamos seu pointer ao definir main: `func main{output_ptr: felt*}():`.

## 2. Mais sobre como os felts sÃ£o interessantes (raros?)

> O felt Ã© o Ãºnico tipo de dado que existe no Cairo, vocÃª pode atÃ© omiti-lo [sua declaraÃ§Ã£o explÃ­cita] (StarkNet Bootcamp - Amsterdam - min 1:14:36).

Embora nÃ£o seja necessÃ¡rio ser especialista nas qualidades matemÃ¡ticas dos felts, Ã© valioso saber como eles funcionam. No Ãºltimo tutorial, os apresentamos pela primeira vez, agora saberemos como eles afetam quando comparamos valores no Cairo.

> A definiÃ§Ã£o de felt, em termos terrestres (a exata estÃ¡ aqui): um inteiro que pode se tornar enorme (mas tem limites). Por exemplo: {...,-4,-3,-2,-1,0,+1,+2,+3,...}. Sim, inclui 0 e nÃºmeros negativos.

Qualquer valor que nÃ£o esteja dentro desse intervalo causarÃ¡ um "overflow": um erro que ocorre quando um programa recebe um nÃºmero, valor ou variÃ¡vel fora do escopo de sua capacidade de manipulaÃ§Ã£o ([Techopedia](https://www.techopedia . com/definition/663/overflow-error#:~:text=In%20computing%2C%20an%20overflow%20error,other%20numeric%20types%20of%20variables.)).

Agora entendemos os limites do felt. Se o valor for 0,5, por exemplo, temos um overflow. Onde experimentaremos overflows com frequÃªncia? Nas divisÃµes. O contrato a seguir (o cÃ³digo completo estÃ¡ em [src/division1.cairo](../../../src/division1.cairo)) divide 9/3, verifica com `assert` que o resultado Ã© 3 e imprime o resultado.

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word

func main{output_ptr: felt*}(){
    tempvar x = 9/3;
    assert x = 3;
    serialize_word(x);

    return();
}

```

AtÃ© agora tudo faz sentido. Mas e se o resultado da divisÃ£o nÃ£o for um inteiro como no contrato a seguir (o cÃ³digo estÃ¡ em [src/division2.cairo](../../../src/division2.cairo))?

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word

func main{output_ptr: felt*}(){
    tempvar x = 10/3;
    assert x = 10/3;
    serialize_word(x);

    return();
}

```

Para comeÃ§ar, imprime o belo nÃºmero ğŸŒˆ no console: `1206167596222043737899107594365023368541035738443865566657697352045290673497`. O que Ã© isso e por que ele retorna para nÃ³s em vez de um ponto decimal considerÃ¡vel?

Na funÃ§Ã£o acima `x` **nÃ£o** Ã© um `floating point`, 3.33, **ni** Ã© um inteiro arredondado para o resultado, 3. Ã‰ um inteiro que multiplicado por 3 nos darÃ¡ 10 de volta ( veja como esta funÃ§Ã£o `3 * x = 10`) ou tambÃ©m `x` pode ser um denominador que retorna 3 (`10 / x = 3`). Vamos ver isso com o seguinte contrato:

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word

func main{output_ptr: felt*}(){
    tempvar x = 10/3;

    tempvar y = 3 * x;
    assert y = 10;
    serialize_word(y);

    tempvar z = 10 / x;
    assert z = 3;
    serialize_word(z);

    return();
}

```

Ao compilar e executar este contrato, obtemos exatamente o que estÃ¡vamos procurando:

```python
Program output:
  10
  3

```
Cairo consegue isso voltando transbordando novamente. NÃ£o vamos entrar em detalhes matemÃ¡ticos. isso Ã© pouco intuitivo, mas nÃ£o se preocupe, podemos deixar aqui.

> Uma vez que vocÃª estÃ¡ escrevendo contratos com Cairo, vocÃª nÃ£o precisa ficar pensando constantemente sobre isso [as peculiaridades dos felts quando estÃ£o em divisÃµes]. Mas Ã© bom estar ciente de como eles funcionam (StarkNet Bootcamp - Amsterdam - min 1:31:00).
>

## **3. Comparando felts ğŸ’ª**

Devido Ã s particularidades dos felts, comparar entre felts nÃ£o Ã© como em outras linguagens de programaÃ§Ã£o (como com `1 < 2`).

Na biblioteca `starkware.cairo.common.math` encontramos funÃ§Ãµes que nos ajudarÃ£o a comparar felts ([link para o repositÃ³rio GitHub](https://github.com/starkware-libs/cairo-lang/blob/master/src /starkware/cairo/common/math.cairo)). Por enquanto vamos usar `assert_not_zero`, `assert_not_equal`, `assert_nn` e `assert_le`. Existem mais recursos para comparar felts nesta biblioteca, recomendo que vocÃª veja o repositÃ³rio do GitHub para explorÃ¡-los. O [seguinte cÃ³digo do StarkNet Bootcamp Amsterdam](https://github.com/lightshiftdev/starknet-bootcamp/blob/main/packages/contracts/samples/04-cairo-math.cairo) ajuda a entender o que cada um faz as funÃ§Ãµes que importamos (alterei um pouco). O cÃ³digo completo estÃ¡ em [src/asserts.cairo](../../../src/asserts.cairo).

```python
%builtins range_check

from starkware.cairo.common.math import assert_not_zero, assert_not_equal, assert_nn, assert_le

func main{range_check_ptr : felt}(){
    assert_not_zero(1);  // not zero
    assert_not_equal(1, 2);  // not equal
    assert_nn(1); // non-negative
    assert_le(1, 10);  // less or equal
    
    return ();
}

```

Simples, certo? SÃ£o apenas maneiras diferentes de fazer asserts.

Mas e se quisermos comparar `10/3 < 10`? Sabemos que isso Ã© verdade, mas tambÃ©m sabemos que o resultado de `10/3` nÃ£o Ã© um nÃºmero inteiro, por isso estÃ¡ fora do intervalo de valores possÃ­veis que os felts podem assumir. HaverÃ¡ overflow e serÃ¡ gerado o grande inteiro, que naturalmente serÃ¡ maior que 10 ou atÃ© mesmo se tornarÃ¡ fora dos possÃ­veis inteiros que um felt pode levar (devido ao tamanho).

De fato, a seguinte funÃ§Ã£o que compara `10/3 < 10` retornarÃ¡ um erro: `AssertionError: a = 2412335192444087475798215188730046737082071476887731133315394704090581346994 is out of range.`

```python
%builtins range_check

from starkware.cairo.common.math import assert_lt

func main{range_check_ptr : felt}(){
    assert_lt(10/3, 10); // less than

    return ();
}

```

Como entÃ£o comparamos `10/3 < 10`? Temos que voltar para nossas aulas de ensino mÃ©dio/faculdade. Vamos apenas remover o 3 do denominador multiplicando tudo por 3; compararÃ­amos `3*10/3 < 3*10` que Ã© o mesmo que `10 < 30`. EntÃ£o, estamos apenas comparando nÃºmeros inteiros e esquecemos como os felts sÃ£o excÃªntricos. A funÃ§Ã£o a seguir Ã© executada sem problemas.

```python
%builtins range_check

from starkware.cairo.common.math import assert_lt

func main{range_check_ptr : felt}(){
    assert_lt(3*10/3, 3*10);

    return ();
}

```

## 4. A natureza dual de assert

Como vimos, `assert` Ã© a chave para a programaÃ§Ã£o no Cairo. Nos exemplos acima, usamos para confirmar uma declaraÃ§Ã£o, `assert y = 10`. Este Ã© um uso comum em outras linguagens de programaÃ§Ã£o como Python. Mas no Cairo quando vocÃª tenta `assert` algo que ainda nÃ£o foi atribuÃ­do, `assert` funciona para atribuir. DÃª uma olhada neste exemplo adaptado de [StarkNet Bootcamp Amsterdam](https://github.com/lightshiftdev/starknet-bootcamp/blob/main/packages/contracts/samples/04-cairo-math.cairo) que tambÃ©m Ã© Ãºtil para consolidar o que vocÃª aprendeu sobre structs no [tutorial anterior](2_basicos_cairo.md). O cÃ³digo completo estÃ¡ em [src/vector.cairo](../../../src/vector.cairo).

```python
 %builtins output

from starkware.cairo.common.serialize import serialize_word

struct Vector2d{
    x : felt,
    y : felt,
}

func add_2d(v1 : Vector2d, v2 : Vector2d) -> (r : Vector2d){
    alloc_locals;

    local res : Vector2d;
    assert res.x = v1.x + v2.x;
    assert res.y = v1.y + v2.y;

    return (r=res);
}

func main{output_ptr: felt*}(){
    
    let v1 = Vector2d(x = 1, y = 2);
    let v2 = Vector2d(x = 3, y = 4);

    let (sum) = add_2d(v1, v2);

    serialize_word(sum.x);
    serialize_word(sum.y);

    return();
}
```

Ao executar `assert res.x = v1.x + v2.x`, o prover do Cairo (mais sobre isso depois) detecta que `res.x` nÃ£o existe e atribui o novo valor `v1.x + v2.x` . Se fÃ´ssemos executar `assert res.x = v1.x + v2.x` novamente, o prover realmente compararia o que encontra atribuÃ­do em `res.x` com o que tentamos atribuir. Ou seja, o uso que jÃ¡ sabÃ­amos.

## 5. Arrays no Cairo

Vamos fechar este tutorial com uma das estruturas de dados mais importantes. Arrays, contÃªm elementos ordenados. Eles sÃ£o muito comuns na programaÃ§Ã£o. Como eles funcionam no Cairo? Vamos aprender a **criar array de matrizes ğŸ™‰**. Sim, o escritor tem background em machine learning. O contrato abaixo estÃ¡ comentado (pode ser encontrado em [src/matrix.cairo](../../../src/matrix.cairo)) e examinaremos apenas a parte dos arrays, pois o leitor jÃ¡ sabe o resto.

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word
from starkware.cairo.common.alloc import alloc

struct Vector{
    elements : felt*,
}

struct Matrix{
    x : Vector,
    y : Vector,
}

func main{output_ptr: felt*}(){

    // Defining an array, my_array, of felts.
    let (my_array : felt*) = alloc();

    // Assigning values to three elements of my_array.  
    assert my_array[0] = 1;
    assert my_array[1] = 2;
    assert my_array[2] = 3;

    // Creating the vectors Vector, by
    // simplicity we use the same my_array for both.
    let v1 = Vector(elements = my_array);
    let v2 = Vector(elements = my_array);

    // Defining an array of Matrix matrices
    let (matrix_array : Matrix*) = alloc();

    // Filling matrix_array with Matrix instances.
    // Each instance of Matrix contains as members
    // Vector instances.
    assert matrix_array[0] = Matrix(x = v1, y = v2);
    assert matrix_array[1] = Matrix(x = v1, y = v2);

    // We use assert to test some values in
    // our matrix_array.
    assert matrix_array[0].x.elements[0] = 1;
    assert matrix_array[1].x.elements[1] = 2;

    // What value do you think it will print? Answer: 3
    serialize_word(matrix_array[1].x.elements[2]);

    return();
}
```

Criamos um array de feltros chamado `my_array`. Assim Ã© definido:

```
let (my_array : felt*) = alloc();
```

Ã© pouco intuitivo em comparaÃ§Ã£o com o quÃ£o fÃ¡cil Ã© em Python e outras linguagens. `my_array : felt*` define uma variÃ¡vel chamada `my_array` que conterÃ¡ um pointer (veja [tutorial anterior](2_basicos_cairo.md)) para um felt (ainda nÃ£o definimos qual felt). Por quÃª? A documentaÃ§Ã£o do Cairo nos ajuda:

> â€œArrays podem ser definidos como um pointer (felt*) para o primeiro elemento do array. Ã€ medida que array Ã© preenchida, os elementos ocupam cÃ©lulas de memÃ³ria contÃ­guas. A funÃ§Ã£o alloc() Ã© usada para definir um segmento de memÃ³ria que se expande em tamanho cada vez que um novo elemento Ã© gravado no array (documentaÃ§Ã£o do Cairo)."
>

EntÃ£o, no caso de `my_array`, colocando o `alloc()` estamos indicando o segmento de memÃ³ria para o qual a expressÃ£o `my_array` aponta (lembre-se que `my_array` Ã© apenas o nome de um pointer, `felt* ` , na memÃ³ria) serÃ¡ expandido cada vez que um novo elemento for escrito em `my_array`.

De fato, se formos [ao repositÃ³rio](https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/alloc.cairo) onde `alloc()` estÃ¡ localizado veremos que ele retorna `(ptr : felt*)`. Ou seja, ele retorna uma tupla de membro Ãºnico que Ã© um `felt*` (um ponteiro para um `felt`). Por ser uma tupla, nÃ³s a recebemos com um `let` e com `my_array : felt*` entre parÃªnteses (veja [basicos de Cairo pt. 2](2_basicos_cairo.md y)). Tudo estÃ¡ fazendo sentido, certo ğŸ™?

Vemos que a definiÃ§Ã£o do nosso array de matrizes Ã© exatamente a mesma, exceto que ao invÃ©s de querer um array de `felt` nÃ³s queremos um de `Matrix`:

```python
let (matrix_array : Matrix*) = alloc();
```

JÃ¡ passamos o complicado ğŸ˜´. Agora vamos ver como preencher nosso array com estruturas `Matrix`. Usamos `assert` e podemos indexar com `[]` a posiÃ§Ã£o do array que queremos alterar ou revisar:

```
assert matrix_array[0] = Matrix(x = v1, y = v2);
```

O que fizemos foi criar um `Matrix(x = v1, y = v2)` e atribuÃ­-lo Ã  posiÃ§Ã£o 0 do nosso `matrix_array`. Lembre-se que comeÃ§amos a contar de 0. Preencher nosso array `felt` Ã© ainda mais trivial: `assert my_array[0] = 1`.

EntÃ£o nÃ³s simplesmente chamamos os elementos dentro do `matrix_array` de diferentes maneiras. Por exemplo, com `matrix_array[1].x.elements[2]` indicamos estas etapas:

1. Chame o segundo, `[1]`, elemento de `matrix_array`. Ou seja, para `Matriz(x = v1, y = v2)`.
2. Chame o `membro` `x` de `Matrix`. Ou seja, para `v1 = Vector(elements = my_array)`.
3. Chame o `membro` `elementos` de `v1`. Ou seja, para `my_array`.
4. Chame o terceiro, `[2]`, elemento de `my_array`. Ou seja, para `3`.

NÃ£o Ã© tÃ£o complicado assim mas Ã© satisfatÃ³rio o suficiente ğŸ¤­.

## **6. ConclusÃ£o**

ParabÃ©ns ğŸš€. NÃ³s mergulhamos no bÃ¡sico do ğŸ– Cairo. Com esse conhecimento vocÃª pode comeÃ§ar a fazer contratos simples no Cairo   .

Nos tutoriais a seguir, aprenderemos mais sobre gerenciamento de memÃ³ria; a common library do Cairo; como funciona o compilador do Cairo; e mais!

Quaisquer comentÃ¡rios ou melhorias, por favor, comente com [@espejelomar](https://twitter.com/espejelomar) ou faÃ§a um PR ğŸŒˆ.