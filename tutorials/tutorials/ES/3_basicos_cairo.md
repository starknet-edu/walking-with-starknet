# Programando en la L2 de Ethereum (pt. 3): BÃ¡sicos de Cairo 2

Antes de comenzar, te recomiendo que prepares tu equipo para programar en Cairo â¤ï¸ con elÂ [primer tutorial](1_instalacion.md), y revises losÂ [bÃ¡sicos de Cairo pt. 1](2_basicos_cairo.md).

Ãšnete a la comunidad de habla hispana de StarkNet ([Linktree](https://linktr.ee/starknet_es) con links a telegram, tutoriales, proyectos, etc.). Este es el tercer tutorial de una serie enfocada en el desarrollo de smart cotracts con Cairo y StarkNet. 

ğŸš€ El futuro de Ethereum es hoy y ya estÃ¡ aquÃ­. Y apenas es el comienzo. Aprende un poco mÃ¡s sobre el ecosistema de Starkware enÂ [este texto corto](https://mirror.xyz/espejel.eth/PlDDEHJpp3Y0UhWVvGAnkk4JsBbJ8jr1oopGZFaRilI).

---

En la tercera parte de la serie de tutoriales bÃ¡sicos de Cairo profundizaremos en conceptos introducidos en laÂ [segunda sesiÃ³n](https://github.com/starknet-edu/walking-with-starknet/blob/master/tutorials/tutorials/ES/2_basicos_cairo.md)Â como losÂ `builtin`, losÂ `felt`Â yÂ `assert`Â y sus variaciones. AdemÃ¡s, introduciremos los arrays. Con lo aprendido en esta sesiÃ³n seremos capaces de crear contratos bÃ¡sicos en Cairo ğŸš€.

## 1. Los builtin y su relaciÃ³n con los pointers

En el siguiente programa estamos multiplicando dos nÃºmeros. El cÃ³digo entero estÃ¡ disponible en [src/multiplication.cairo](../../../src/multiplication.cairo). AhÃ­ encontrarÃ¡s el cÃ³digo correctamente comentado.

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word

func mult_two_nums(num1, num2) -> (prod : felt){
    return(prod = num1 * num2);
}

func main{output_ptr: felt*}(){
    let (prod) = mult_two_nums(2,2);
    serialize_word(prod);
    return ();
}
```

Â¿Recuerdas que introdujimos losÂ `builtins`Â en la sesiÃ³n pasada junto con los argumentos implÃ­citos?

CadaÂ `builtin`Â te da el derecho a usar un pointer que tendrÃ¡ el nombre delÂ `builtin`Â + â€œ`_ptr`â€. Por ejemplo, el builtin output, que definimosÂ `%builtins output`Â al inicio de nuestro contrato, nos da derecho a usar el pointerÂ `output_ptr`. ElÂ `builtin`Â `range_check`Â nos permite usar el pointerÂ `range_check_ptr`. Estos pointers suelen usarse como argumentos implÃ­citos que se actualizan automÃ¡ticamente durante una funciÃ³n.

En la funciÃ³n para multiplicar dos nÃºmeros usamosÂ `%builtins output`Â y, posteriormente, utilizamos su pointer al definir main:Â `func main{output_ptr: felt*}():`.

## 2. MÃ¡s sobre lo interesante (raros?) que son los felts

> El felt es el Ãºnico tipo de datos que existe en Cairo, incluso puedes omitirlo [su declaraciÃ³n explÃ­cita] (StarkNet Bootcamp - Amsterdam - min 1:14:36).

Si bien no es necesario ser un@ expert@ en las cualidades matemÃ¡ticas de los felts, es valioso conocer cÃ³mo funcionan. En el tutorial pasado los introdujimos por primera vez, ahora conoceremos cÃ³mo afectan cuando comparamos valores en Cairo.

> La definiciÃ³n de un felt, en tÃ©rminos terrestres (la exacta estaÂ aquÃ­): un nÃºmero entero que puede llegar a ser enorme (pero tiene lÃ­mites). Por ejemplo:Â {...,-4,-3,-2,-1,0,+1,+2,+3,...}. SÃ­, incluye 0 y nÃºmeros negativos.

Cualquier valor que no se encuentre dentro de este rango causarÃ¡ un â€œoverflowâ€: un error que ocurre cuando un programa recibe un nÃºmero, valor o variable fuera del alcance de su capacidad para manejar ([Techopedia](https://www.techopedia.com/definition/663/overflow-error#:~:text=In%20computing%2C%20an%20overflow%20error,other%20numerical%20types%20of%20variables.)).

Ahora entendemos los lÃ­mites de los felt. Si el valor es 0.5, por ejemplo, tenemos un overflow. Â¿DÃ³nde experimentaremos overflows frecuentemente? En las divisiones. El siguiente contrato (el cÃ³digo completo estÃ¡ en [src/division1.cairo](../../../src/division1.cairo)) divide 9/3, revisa conÂ `assert`Â que el resultado sea 3, e imprime el resultado.

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word

func main{output_ptr: felt*}(){
    tempvar x = 9/3;
    assert x = 3;
    serialize_word(x);

    return();
}

```

Hasta ahora todo hace sentido. Â¿Pero quÃ© pasa si el resultado de la divisiÃ³n no es un entero como en el siguiente contrato (el cÃ³digo estÃ¡ en [src/division2.cairo](../../../src/division2.cairo))?

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word

func main{output_ptr: felt*}(){
    tempvar x = 10/3;
    assert x = 10/3;
    serialize_word(x);

    return();
}

```

Para empezar, nos imprime en consola el hermoso nÃºmero ğŸŒˆ:Â `1206167596222043737899107594365023368541035738443865566657697352045290673497`. Â¿QuÃ© es esto y por quÃ© nos lo retorna en vez de un apreciable punto decimal?

En la funciÃ³n arribaÂ `x`Â **no**Â es unÂ `floating point`, 3.33,Â **ni**Â es un entero redondeado con el resultado, 3. Es un entero que multiplicado por 3 nos darÃ¡ 10 de vuelta (se ve como esta funciÃ³nÂ `3 * x = 10`) o tambiÃ©nÂ `x`Â puede ser un denominador que nos devuelva 3 (`10 / x = 3`). Veamos esto con el siguiente contrato:

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word

func main{output_ptr: felt*}(){
    tempvar x = 10/3;

    tempvar y = 3 * x;
    assert y = 10;
    serialize_word(y);

    tempvar z = 10 / x;
    assert z = 3;
    serialize_word(z);

    return();
}

```

Al compilar y correr este contrato obtenemos exactamente lo que buscabamos:

```python
Program output:
  10
  3

```

Cairo logra esto al volver al realizar un overflowing de nuevo. No entremos en detalles matemÃ¡ticos. Esto es algo poco intuitivo pero no te preocupes, hasta aquÃ­ lo podemos dejar.

> Una vez que estÃ¡s escribiendo contratos con Cairo no necesitas estar pensando constantemente en esto [las particularidades de los felts cuando estÃ¡n en divisiones]. Pero es bueno estar consciente de cÃ³mo funcionan (StarkNet Bootcamp - Amsterdam - min 1:31:00).
> 

## **3. Comparando felts ğŸ’ª**

Debido a las particularidades de los felts, comparar entre felts no es como en otros lenguajes de programaciÃ³n (como conÂ `1 < 2`).

En la librerÃ­aÂ `starkware.cairo.common.math`Â encontramos funciones que nos servirÃ¡n para comparar felts ([link a repositorio en GitHub](https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/math.cairo)). Por ahora usaremosÂ `assert_not_zero`,Â `assert_not_equal`,Â `assert_nn`Â yÂ `assert_le`. Hay mÃ¡s funciones para comparar felts en esta librerÃ­a, te recomiendo que veas el repositorio de GitHub para explorarlas. ElÂ [siguiente cÃ³digo del Bootcamp de StarkNet en Amsterdam](https://github.com/lightshiftdev/starknet-bootcamp/blob/main/packages/contracts/samples/04-cairo-math.cairo)Â sirve para entender lo que hace cada una de las funciones que importamos (lo alterÃ© ligeramente). El cÃ³digo completo estÃ¡ en [src/asserts.cairo](../../../src/asserts.cairo).

```python
%builtins range_check

from starkware.cairo.common.math import assert_not_zero, assert_not_equal, assert_nn, assert_le

func main{range_check_ptr : felt}(){
    assert_not_zero(1);  // not zero
    assert_not_equal(1, 2);  // not equal
    assert_nn(1); // non-negative
    assert_le(1, 10);  // less or equal
    
    return ();
}

```

Â¿Sencillo, cierto? Solo son formas diferentes de hacer asserts.

Â¿Pero quÃ© pasa si queremos comparar `10/3 < 10`? Sabemos que esto es cierto, pero tambiÃ©n sabemos que el resultado de la divisiÃ³n `10/3` no es un entero por lo que cae fuera del rango de posibles valores que pueden tomar los felts. HabrÃ¡ overflow y se generarÃ¡ un valor que resultarÃ¡ estar fuera de los enteros posibles que un felt puede tomar (por lo grande que es).

n efecto la siguiente funciÃ³n que compara `10/3 < 10` nos retornarÃ¡ un error: `AssertionError: a = 2412335192444087475798215188730046737082071476887731133315394704090581346994 is out of range.`

```python
%builtins range_check

from starkware.cairo.common.math import assert_lt

func main{range_check_ptr : felt}(){
    assert_lt(10/3, 10); // less than

    return ();
}

```

Â¿CÃ³mo hacemos entonces para compararÂ `10/3 < 10`? Tenemos que volver a nuestras clases de secundaria/colegio. Simplemente eliminemos el 3 del denominador al multiplicar todo por 3; compararÃ­amosÂ `3*10/3 < 3*10`Â que es lo mismo queÂ `10 < 30`. AsÃ­ solo estamos comparando enteros y nos olvidamos de lo exÃ©ntricos que son los felt. La siguiente funciÃ³n corre sin problema.

```python
%builtins range_check

from starkware.cairo.common.math import assert_lt

func main{range_check_ptr : felt}(){
    assert_lt(3*10/3, 3*10);

    return ();
}

```

## 4. La doble naturaleza de assert

Como hemos visto, `assert` es clave para la programaciÃ³n en Cairo. En los ejemplos arriba lo utilizamos para confirmar una declaraciÃ³n, `assert y = 10`. Este es un uso comÃºn en otros lenguajes de programaciÃ³n como Python. Pero en Cairo cuando tratas de `assert` algo que no estÃ¡ asignado aÃºn, `assert` funciona para asignar. Mira estÃ© ejemplo adaptado del [Bootcamp de StarkNet en Amsterdam](https://github.com/lightshiftdev/starknet-bootcamp/blob/main/packages/contracts/samples/04-cairo-math.cairo) que tambiÃ©n nos sirve para afianzar lo aprendido sobre las structs en el [tutorial pasado](2_basicos_cairo.md). El cÃ³digo completo estÃ¡ en [src/vector.cairo](../../../src/vector.cairo). 

```python
 %builtins output

from starkware.cairo.common.serialize import serialize_word

struct Vector2d{
    x : felt,
    y : felt,
}

func add_2d(v1 : Vector2d, v2 : Vector2d) -> (r : Vector2d){
    alloc_locals;

    local res : Vector2d;
    assert res.x = v1.x + v2.x;
    assert res.y = v1.y + v2.y;

    return (r=res);
}

func main{output_ptr: felt*}(){
    
    let v1 = Vector2d(x = 1, y = 2);
    let v2 = Vector2d(x = 3, y = 4);

    let (sum) = add_2d(v1, v2);

    serialize_word(sum.x);
    serialize_word(sum.y);

    return();
}
```

Al correrÂ `assert res.x = v1.x + v2.x`, el prover (mÃ¡s sobre esto mÃ¡s adelante) de Cairo detecta queÂ `res.x`Â no existe por lo que le asigna el nuevo valorÂ `v1.x + v2.x`. Si volvieramos a correrÂ `assert res.x = v1.x + v2.x`, el prover sÃ­ compararÃ­a lo que encuentra asignado enÂ `res.x`Â con lo que intentamos asignar. Es decir, el uso que ya conocÃ­amos.

## 5. Arrays en Cairo

Cerremos este tutorial con una de las estructura de datos mÃ¡s importantes. Los arrays, arreglos en espaÃ±ol, contienen elementos ordenados. Son muy comunes en programaciÃ³n. Â¿CÃ³mo funcionan en Cairo? AprendamosÂ **creando un array de matrices ğŸ™‰**. SÃ­, el escrito tiene un background en machine learning. El contrato abajo estÃ¡ comentado (se encuentra en [src/matrix.cairo](../../../src/matrix.cairo)) y examinaremos unicamente la parte de los arrays pues el lector ya conoce el resto.

```python
%builtins output

from starkware.cairo.common.serialize import serialize_word
from starkware.cairo.common.alloc import alloc

struct Vector{
    elements : felt*,
}

struct Matrix{
    x : Vector,
    y : Vector,
}

func main{output_ptr: felt*}(){

    // Defining an array, my_array, of felts.
    let (my_array : felt*) = alloc();

    // Assigning values â€‹â€‹to three elements of my_array.  
    assert my_array[0] = 1;
    assert my_array[1] = 2;
    assert my_array[2] = 3;

    // Creating the vectors Vector, by
    // simplicity we use the same my_array for both.
    let v1 = Vector(elements = my_array);
    let v2 = Vector(elements = my_array);

    // Defining an array of Matrix matrices
    let (matrix_array : Matrix*) = alloc();

    // Filling matrix_array with Matrix instances.
    // Each instance of Matrix contains as members
    // Vector instances.
    assert matrix_array[0] = Matrix(x = v1, y = v2);
    assert matrix_array[1] = Matrix(x = v1, y = v2);

    // We use assert to test some values â€‹â€‹in
    // our matrix_array.
    assert matrix_array[0].x.elements[0] = 1;
    assert matrix_array[1].x.elements[1] = 2;

    // What value do you think it will print? Answer: 3
    serialize_word(matrix_array[1].x.elements[2]);

    return();
}
```

Creamos un array de felts llamadoÂ `my_array`. Esta es la forma en que se define:

```
let (my_array : felt*) = alloc();
```

Es poco intuitivo en comparaciÃ³n con lo fÃ¡cil que es en Python y otros lenguajes. `my_array : felt*` define una variable llamada `my_array` que contendrÃ¡ un pointer (ver [tutorial pasado](2_basicos_cairo.md)) a un felt (aÃºn no definimos a quÃ© felt). Â¿Por quÃ©? La documentaciÃ³n de Cairo nos ayuda:

> â€œLos arrays se pueden definir como un pointer (felt*) al primer elemento del array.Â A medida que se llena el array, los elementos ocupan celdas de memoria contiguas. La funciÃ³nÂ alloc()Â se usa para definir un segmento de memoria que expande su tamaÃ±o cada vez que se escribe un nuevo elemento en el array (documentaciÃ³nÂ de Cairo)â€.
> 

Entonces, en el caso deÂ `my_array`, al colocar elÂ `alloc()`Â estamos indicando que el segmento de memoria al que la expresiÃ³nÂ `my_array`Â apunta (recuerda queÂ `my_array`Â es solo el nombre de un pointer,Â `felt*`, en memoria) serÃ¡ expandido cada vez que se escriba un nuevo elemento enÂ `my_array`.

De hecho, si pasamos [al repo](https://github.com/starkware-libs/cairo-lang/blob/master/src/starkware/cairo/common/alloc.cairo) donde se encuentra `alloc()` veremos que retorna `(ptr : felt*)`. Es decir, nos regresa una tupla de un solo miembro que es un `felt*` (un pointer a un `felt`). Por ser una tupla la recibimos con un `let` y con `my_array : felt*` entre parÃ©ntesis (ver [bÃ¡sicos de Cairo pt. 2](2_basicos_cairo.md)). Todo va haciendo sentido, Â¿cierto ğŸ™?

Vemos que la definiciÃ³n de nuestro array de matrices es exactamente igual salvo que en vez de querer un array deÂ `felt`Â queremos uno deÂ `Matrix`:

```python
let (matrix_array : Matrix*) = alloc();
```

Ya pasamos lo complicado ğŸ˜´. Ahora veamos cÃ³mo rellenar nuestro array con estructuras `Matrix`. Usamos `assert` y podemos indexar con `[]` la posiciÃ³n del array que queremos alterar o revisar:

```
assert matrix_array[0] = Matrix(x = v1, y = v2);
```

Lo que hicimos fue crear unaÂ `Matrix(x = v1, y = v2)`Â y asignarla a la posiciÃ³n 0 de nuestraÂ `matrix_array`. Recuerda que empezamos a contar desde 0. Rellenar nuestro array deÂ `felt`Â es aÃºn mÃ¡s trivial:Â `assert my_array[0] = 1`.

DespuÃ©s simplemente llamamos de diferentes maneras a elementos dentro deÂ `matrix_array`. Por ejemplo, conÂ `matrix_array[1].x.elements[2]`Â indicamos estos pasos:

1. Llama al segundo,Â `[1]`, elemento deÂ `matrix_array`. Es decir, aÂ `Matrix(x = v1, y = v2)`.
2. Llama alÂ `member`Â `x`Â deÂ `Matrix`. Es decir, aÂ `v1 = Vector(elements = my_array)`.
3. Llama alÂ `member`Â `elements`Â deÂ `v1`. Es decir, aÂ `my_array`.
4. Llama al tercer,Â `[2]`, elemento deÂ `my_array`. Es decir, aÂ `3`.

No es tan complicado pero es lo suficientemente satisfactorio ğŸ¤­.

## **6. ConclusiÃ³n**

Felicidades ğŸš€. Hemos profundizado en los bÃ¡sicos de ğŸ– Cairo. Con este conocimiento puedes comenzar a hacer contratos sencillos en Cairo ğŸ¥³.

En los siguientes tutoriales aprenderemos mÃ¡s sobre los el manejo de la memoria; la common library de cairo; cÃ³mo funciona el compilador de Cairo; y mÃ¡s!

Cualquier comentario o mejora por favor comentar conÂ [@espejelomar](https://twitter.com/espejelomar)Â o haz un PR ğŸŒˆ.
